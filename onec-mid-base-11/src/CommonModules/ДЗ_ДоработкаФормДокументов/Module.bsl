#Область ПрограммныйИнтерфейс

Процедура ДополнитьФорму(Форма) Экспорт

	//Сморгун А.В. Процедура доработки формы
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" ИЛИ 
		ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента"	Тогда 
		
		ДобавитьПолеПередСуммойДокумента(Форма);
		
	ИначеЕсли 
		
		ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" ИЛИ
		ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		
		ДобавитьПолеВГруппаШапкаПраво(Форма);
		
	ИначеЕсли 
		
		ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		
		ДобавитьПолеВГруппаШапкаПраво(Форма);
		ДобавитьГруппуПолеСкидка(Форма);
		
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПолеВГруппаШапкаПраво(Форма)
	
	//Сморгун А.В. Добавление поля Контактное лицо в документы Поступления, реализации и заказа
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо",Тип("ПолеФормы"),Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";	
	
КонецПроцедуры

Процедура ДобавитьПолеПередСуммойДокумента(Форма)

	//Сморгун А.В. Добавление поля Контактное лицо в документы Оплаты

 	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо",Тип("ПолеФормы"),,Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";	
	
КонецПроцедуры

Процедура ДобавитьГруппуПолеСкидка(Форма)
	
	// Сморгун А.В. Добавление группыСкидка для согласованной скидки в Документ Заказа 
	
	ПолеГруппа = Форма.Элементы.Добавить("ГруппаСкидка",Тип("ГруппаФормы"),Форма.Элементы.ГруппаШапкаЛево);
	ПолеГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ПолеГруппа.Отображение = ОтображениеОбычнойГруппы.Нет;
	ПолеГруппа.ОтображатьЗаголовок = Ложь;
	ПолеГруппа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка",Тип("ПолеФормы"),ПолеГруппа);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении","СогласованнаяСкидкаПриИзменении");
	
КонецПроцедуры

#КонецОбласти